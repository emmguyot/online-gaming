; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Skin Bois InCrEG pour l'aire du Club des Beloteux"
!define CLIENT_NAME "Club des Beloteux"
!define PRODUCT_VERSION "1.1"
!define PRODUCT_PUBLISHER "InCrEG sarl"
!define PRODUCT_WEB_SITE "http://www.increg.com/aireDeJeu.jsp"
!define CLIENT_WEB_SITE "http://www.beloteux.com"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"


SetCompressor lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "favicon.ico"
!define MUI_UNICON "favicon.ico"

; Welcome page
!define MUI_WELCOMEFINISHPAGE_BITMAP "welcome.bmp"
!define MUI_UNWELCOMEFINISHPAGE_BITMAP "welcome.bmp"
!define MUI_WELCOMEPAGE_TITLE "Installation de $(^NameDA)"
!define MUI_FINISHPAGE_TITLE "Fin de l'installation"
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "I:\Administratif\Contrat\ContratSkin.txt"
; Choix de ce qui est installé
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "${PRODUCT_NAME}"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "French"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

!include "strfunc.nsh"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "../../../../../salon_serveur/boutique/download/SkinBois_InCrEG.exe"
InstallDir "$PROGRAMFILES\InCrEG\SkinBois"
ShowInstDetails nevershow
ShowUnInstDetails nevershow

Section "Fichiers principaux" SEC00

  SetOutPath "$INSTDIR\conf"
  SetOverwrite ifnewer
  File "conf\beloteux.xml"
  File "conf\son.xml"
  SetOutPath "$INSTDIR\images"
  File "images\*.gif"
  File "images\*.jpg"
  SetOutPath "$INSTDIR\images\son"
  File "images\son\*.gif"
  ; Config par défaut
  SetOverwrite off
  StrCmp $PROFILE "" oldWin
    SetOutPath "$PROFILE\.java"
    File "properties141_02"
    goto fin
  oldWin:
    SetOutPath "$WINDIR\.java"
    File /oname="properties" "properties141_02"
  fin:
SectionEnd

Section "Smileys" SEC01
  SetOutPath "$INSTDIR\conf"
  SetOverwrite ifnewer
  File "conf\smiley.xml"
  SetOutPath "$INSTDIR\images\anime"
  File "images\anime\*.gif"
  SetOutPath "$INSTDIR\images\statique"
  File "images\statique\*.gif"
SectionEnd

Section -Core
  SetOutPath "$INSTDIR\"
  SetOverwrite ifnewer
  File /oname="Licence.txt" "I:\Administratif\Contrat\ContratSkin.txt"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  WriteIniStr "$INSTDIR\${CLIENT_NAME}.url" "InternetShortcut" "URL" "${CLIENT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Aires de jeu InCrEG.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Site Web ${CLIENT_NAME}.lnk" "$INSTDIR\${CLIENT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Désinstaller.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "${PRODUCT_STARTMENU_REGVAL}" "$ICONS_GROUP"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Function FindFiles
  Exch $R5 # callback function
  Exch
  Exch $R9 # Files
  Exch 2
  Exch $R0 # directory
  Push $R1
  Push $R2
  Push $R3
  Push $R6

  Push $R0 # first dir to search

  StrCpy $R3 1

  nextDir:
    Pop $R0
    IntOp $R3 $R3 - 1
    ClearErrors
    FindFirst $R1 $R2 "$R0\$R9"
    IfErrors done
    nextFile:
      StrCmp $R2 "." gotoNextFile
      StrCmp $R2 ".." gotoNextFile

      ; R2 = nom du fichier
      Push "$R0\$R2"
      Call $R5
      Pop $R6
      StrCmp $R6 "stop" 0 isDir
        loop:
          StrCmp $R3 0 done
          Pop $R0
          IntOp $R3 $R3 - 1
          Goto loop

      isDir:
        IfFileExists "$R0\$R2\*.*" 0 gotoNextFile
          IntOp $R3 $R3 + 1
          Push "$R0\$R2"

  gotoNextFile:
    FindNext $R1 $R2
    IfErrors 0 nextFile

  done:
    FindClose $R1
    StrCmp $R3 0 0 nextDir

  Pop $R6
  Pop $R3
  Pop $R2
  Pop $R1
  Pop $R0
  Pop $R9
  Pop $R5
FunctionEnd

!macro CallFindFiles DIR FILES CBFUNC
Push "${DIR}"
Push "${FILES}"
Push $0
GetFunctionAddress $0 "${CBFUNC}"
Exch $0
Call FindFiles
!macroend

${StrStr}
${StrRep}

Function FindConfig
  Pop $3
  Push $R0

  ClearErrors
  FileOpen $0 "$3" "r"
  GetTempFileName $R0
  FileOpen $1 $R0 "w"
  StrCpy $7 ""
  GetFullPathName /SHORT $8 $INSTDIR
  ${StrRep} $8 $8 "\" "/"
  loop3:
          FileRead $0 $2
          IfErrors done3
          ; Recherche de :
          ; javaplugin.jre.params
          ; Incompatible 1.5 car -cp=... est utilisé

          ${StrStr} $6 $2 "javaplugin.jre.params"
          StrCmp $6 "" telquel 0
          ; Violent : Annule et remplace toutes les options
          FileWrite $1 "javaplugin.jre.params=-cp $8$\r$\n"
          StrCpy $7 "1"
          goto loop3
          telquel:
          ; Tel quel
          FileWrite $1 $2
          Goto loop3

  done3:
  StrCmp $7 "" 0 clean
  FileWrite $1 "javaplugin.jre.params=-cp $8$\r$\n"
  clean:
  FileClose $0
  FileClose $1
  Delete "$3"
  CopyFiles /SILENT $R0 "$3"
  Delete $R0

  Pop $R0
  Push "go"
FunctionEnd

Section -SEC02
  StrCmp $PROFILE "" oldWin
    !insertmacro CallFindFiles "$PROFILE\.java" properties* FindConfig
    !insertmacro CallFindFiles "$PROFILE\Application Data\Sun\Java\Deployment" deployment.properties FindConfig
    !insertmacro CallFindFiles "$PROFILE\.java\Sun\Java\Deployment" deployment.properties FindConfig
    return
  oldWin:
    !insertmacro CallFindFiles "$WINDIR\.java" properties* FindConfig
    !insertmacro CallFindFiles "$WINDIR\Application Data\Sun\Java\Deployment" deployment.properties FindConfig
    !insertmacro CallFindFiles "$WINDIR\.java\Sun\Java\Deployment" deployment.properties FindConfig
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) a été désinstallé avec succès de votre ordinateur."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Êtes-vous certains de vouloir désinstaller totalement $(^Name) et tous ses composants ?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ReadRegStr $ICONS_GROUP ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "${PRODUCT_STARTMENU_REGVAL}"

  RMDir /r "$SMPROGRAMS\$ICONS_GROUP"
  RMDir /r "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  SetAutoClose true
SectionEnd
